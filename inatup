#!/usr/bin/gawk -f

# iNat-up
# For loading observations to iNaturalist
# by Cam Webb <cw@camwebb.info> 2017
# License: CC:C0

@include "lib/JSON_include.awk"
@include "lib/util.awk"
@include "lib/config.awk"  

BEGIN{
  
  # Init
  user_config();
  init_json();
  DEBUG = 0;

  ## For each collection/observation
  while ((getline < INFILE) > 0) {
    # Re-init
    taxon = taxoninfo = testtaxon = testrank = testtaxonid          \
      = date = longlat = elev = collcode = idby = indiv = iddate        \
      = loc = collectors = sciname = cmd = files = nfiles = newphoto \
      = newid = place = ""; desc = DESC; fields = 0;

    # Skip the header
    if ((HEADER) && (FNR == 1)) continue;
    
    if (!$fCollCode) {
      log("Record w/o collcode. Skipping\n");
      continue;
    }

    log("Coll: " $fCollCode);

    # Test for photos
    cmd = "find img/" $fCollCode " -type f | sort";
    RS="\x04";
    cmd | getline files;
    close(cmd); RS="\n";
    
    if (!files) {
      log("  No directory of images. Skipping\n");
      continue;
    }
    
    # Check for the name in iNat
    taxon = $fName;
    if (taxon) {
      # clean
      gsub(/^\ +/,"",taxon); gsub(/\ +$/,"",taxon);
      gsub(/\ +/," ",taxon); 
      print "  Taxon: " taxon >> "upload.log" ;  close("upload.log");
      print "  Querying taxon" > "/dev/stderr"; close("/dev/stderr");

      parse_json(curl("GET", "Accept: application/json",    \
                      "--data-urlencode 'q=" taxon "'",
                      "http://api.inaturalist.org/v1/taxa/autocomplete" ));
    
      testtaxon = gensub(/"/,"","G", JSONPATH["\"results\"",0,"\"name\""]);
      testrank = gensub(/"/,"","G", JSONPATH["\"results\"",0,"\"rank\""]);
      testtaxonid = gensub(/"/,"","G", JSONPATH["\"results\"",0,"\"id\""]);
      if (taxon == testtaxon) {
        print "  Taxon found: " testtaxon "; rank: " testrank"; ID: " \
          testtaxonid >> "upload.log";  close("upload.log");
        taxoninfo = "--data-urlencode 'observation[taxon_id]=" testtaxonid "'";
      }
      else {
        print "  Taxon not found" >> "upload.log";  close("upload.log");
        taxoninfo = "--data-urlencode 'observation[species_guess]=" taxon "'";
      }
    }
    else {
      print "  Record w/o taxon" >> "upload.log";  close("upload.log");
      taxoninfo = "--data-urlencode 'observation[species_guess]=Plantae'";
    }

    # Elements of data
    # date and TZ
    if ($fCollDate) {
      date = "--data-urlencode 'observation[observed_on_string]=" $fCollDate "'" \
        " --data-urlencode 'observation[time_zone]=Osaka'";
    }
    # Long, Lat
    if (($fLat ~ /^[0-9]+.[0-9]+$/) && ($fLong ~ /^[0-9]+.[0-9]+$/)) {
      longlat = "--data-urlencode 'observation[latitude]=" $fLat \
        "' --data-urlencode 'observation[longitude]=" $fLong            \
        "' --data-urlencode 'observation[positional_accuracy]=50'";
      # default accuracy
    }
    else {
      print "  Record w/o good long/lat. Skipping\n" >> "upload.log";
      close("upload.log");
      print "  Record w/o good long/lat. Skipping" >> "/dev/stderr";
      close("/dev/stderr");
      continue;
    }
    
    # Elevation
    if ($fElev) {
      elev = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7588' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fElev " m'" ;
    }
    
    # Collection code
    collcode = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7584' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fCollCode "'" ;
    desc = DESC " Collection code: " $fCollCode ".";

    # Individual id
    if ($fIndiv) {
      indiv = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=6934' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fIndiv "'" ;
    }
    
    # good det
    if ($fSciName) {
      sciname = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7591' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fSciName "'" ;
    }
    
    # det by
    if ($fDetBy) {
      idby = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7589' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fDetBy "'" ;
    }
    else {
      idby = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7589' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=S. Tagane'" ;
    }
    
    # det date
    if ($fDetDate) {
      iddate = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7590' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fDetDate "'" ;
    }
    else if ($fCollDate) {
      iddate = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7590' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fCollDate "'" ;
    }
    
    # Locality
    if ($fLoc) {
      loc = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7587' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fLoc "'" ;
    }
    
    # Collectors
    if ($fCollBy) {
      collectors = "--data-urlencode 'observation[observation_field_values_attributes][" fields "][observation_field_id]=7262' --data-urlencode 'observation[observation_field_values_attributes][" fields++ "][value]=" $fCollBy "'" ;
    }
    
    # Description
    desc = "--data-urlencode 'observation[description]=" desc "'" ;

    print "  Making obs" > "/dev/stderr"; close("/dev/stderr");
    parse_json(curl("POST", "Authorization: Bearer " AUTHTOKEN,         \
                    (taxoninfo " " date " " longlat " " elev " " collcode \
                     " " indiv " " idby " " iddate " " loc " " collectors \
                     " " sciname " " desc),                   \
                    "https://www.inaturalist.org/observations.json"));
    
    newid = JSONPATH[0,"\"id\""];
    place = JSONPATH[0,"\"place_guess\""];
    print "  New Obs id: " newid >> "upload.log";
    # for checking long/lat:
    print "  Place: " place >> "upload.log";  close("upload.log");

    # Upload photos (already checked above)
    nfiles = split(files, file, "\n");
    for (i = 1 ; i < nfiles; i++) {
      print "  Photo " i ": " file[i] >> "upload.log";  close("upload.log");
      print "  Uploading photo " i > "/dev/stderr"; close("/dev/stderr");

      newphoto = curl("POST", "Authorization: Bearer " AUTHTOKEN,       \
                      "-F 'observation_photo[observation_id]=" newid \
                      "' -F 'file=@" file[i] "'",\
                      "https://www.inaturalist.org/observation_photos");
      
      if (newphoto ~ /[Ee]rror/) {
        print "    Photo error!" >> "upload.log";
        close("upload.log");
      }
    }
    delete file;
    # make lookup table:
    print $fCollCode "|" newid "\n" >> "upload.log";  close("upload.log");
  }
}
